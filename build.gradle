import java.nio.file.Paths

plugins {
    id 'application'
    id 'checkstyle'
    id 'org.beryx.runtime' version '1.12.5'
}

group 'com.touchup.kiosk'
version '1.0'

repositories {
    mavenCentral()
}

ext {
    checkstyleVersion = "8.44"
    daggerVersion = "2.37"
    guavaVersion = "30.1.1-jre"
    guiceVersion = "5.0.1"
    ikonliVersion = "12.2.0"
    javafxVersion = "16"
    junitVersion = "5.7.2"
    log4jVersion = "2.14.1"
    migLayoutVersion = "11.0"
    mvvmfxVersion = "1.8.0"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

application {
    mainClass.set("com.touchup.kiosk.Kiosk")
    applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8', '-XX:MaxHeapFreeRatio=40']
}

checkstyle {
    toolVersion "${checkstyleVersion}"
    configFile rootProject.file('config/checkstyle/checkstyle.xml')
}

dependencies {
    implementation "de.saxsys:mvvmfx:${mvvmfxVersion}"

    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "com.google.inject:guice:${guiceVersion}"
    implementation "com.miglayout:miglayout-javafx:${migLayoutVersion}"
    implementation "de.saxsys:mvvmfx-guice:${mvvmfxVersion}"
    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    implementation "org.kordamp.ikonli:ikonli-fontawesome5-pack:${ikonliVersion}"
    implementation "org.kordamp.ikonli:ikonli-javafx:${ikonliVersion}"

    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

runtime {
    imageDir = file("$buildDir/runtime-image")
    modules = ['java.logging', 'java.management', 'java.sql', 'javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.graphics']
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    jpackage {
        imageOptions = ['--icon', Paths.get("$rootProject.buildDir", "resources", "main", "images", "app-icon.ico").toFile()]
        installerType = 'msi'
        installerOptions = ['--vendor','TouchUp Auto Care', '--win-dir-chooser', '--win-per-user-install', '--win-shortcut']
    }
}

test {
    useJUnitPlatform()
}